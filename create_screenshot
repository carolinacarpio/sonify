#!/usr/local/bin/fish

# Parses README to get code for example, runs code, and extracts a screenshot.
# I could probably avoid this mess if I learned Sphinx! Requires the fish shell
# (https://fishshell.com/) to run.

set README_FILENAME README.rst
set SCREENSHOT_FILENAME screenshot.png

set BEGIN_DELIMITER '.. __BEGIN__'
set END_DELIMITER '.. __END__'
set LINE_IGNORE_PATTERN '.. *'

set FRAME 14
set PRE_CMDS 'import matplotlib as m' 'm.rcParams.update(m.rcParamsDefault)'
set CONDA_ENV sonify

# Extract Python example from README
set file (cat $README_FILENAME)
for line in $file
  if test $line = $BEGIN_DELIMITER
    set inside
  end
  if test $line = $END_DELIMITER
    set -e inside
  end
  if set -q inside
    if not string match -q $LINE_IGNORE_PATTERN $line
      set call (string join '' $call (string trim $line) '\n')
    end
  end
end

conda activate $CONDA_ENV

echo (string join '; ' $PRE_CMDS 'exec("'$call'")') | python
set frame (math $FRAME - 1)
ffmpeg -i *.mp4 -vf "select=eq(n\,$frame)" -vframes 1 $SCREENSHOT_FILENAME

conda deactivate

rm *.mp4
